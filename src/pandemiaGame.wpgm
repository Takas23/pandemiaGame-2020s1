import wollok.game.*
import simulacion.*
import agente.*

object pandemiaGameConfig {
	method height() = 4
	method width() = 6
	method title() = "Simulacion"
	
	method configureGame() {
		self.configureGameParameters()
		self.agregarManzanas()
		self.configureTeclas()
		game.addVisual(agente)
//HECHO agregar las teclas de acciones
//HECHO despuÃ©s, al agente de salud y sus teclas de acciones y movimiento
	}
		
	method configureGameParameters() {
		game.height(self.height())
		game.width(self.width())
		game.title(self.title())
	}
	
	method agregarManzanas() {
		(1..game.width()).forEach({x => 
			(1..game.height()).forEach({y => 
				const nuevaManzana = simulacion.crearManzana()
				nuevaManzana.position(game.at(x-1,y-1))
				simulacion.agregarManzana(nuevaManzana)
				game.addVisual(nuevaManzana)
			})
		})
	}
	
 	method configureTeclas() {
		keyboard.m().onPressDo({game.say(agente, "Viven " + agente.manzanaActual().personasQueViven() + " personas.")})
		keyboard.n().onPressDo({simulacion.nuevoInfectado()})
		keyboard.b().onPressDo({simulacion.manzanas().forEach({man => man.pasarUnDia()})})
		keyboard.right().onPressDo({agente.moveEste()})
		keyboard.left().onPressDo({agente.moveOeste()})
		keyboard.up().onPressDo({agente.moveNorte()})
		keyboard.down().onPressDo({agente.moveSur()})
		keyboard.z().onPressDo({agente.aislarInfectados()})
		keyboard.x().onPressDo({agente.cuarentenarManzana()})
	}
	
}

program pandemiaGame {
	pandemiaGameConfig.configureGame()
	game.start()
}
